class Math {
    static Array twoToThe;
    static int divisionTemp;

    function void init() {
        var int i, v;

        let twoToThe = Memory.alloc(16);
        let twoToThe[0] = 1;
        let i = 1;
        let v = 2;
        while (i < 16) {
            let twoToThe[i] = v;
            let v = v + v;
            let i = i + 1;
        }

        return;
    }

    function void getTwoToThe(int n) {
        return twoToThe[n];
    }

    function int abs(int x) {
        if (Math.bit(x, 15)) {
            return -x;
        } else {
            return x;
        }
    }

    function int multiply(int x, int y) {
        var int sum, i;

        while (i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + x;
            }
            let x = x + x;
            let i = i + 1;
        }

        return sum;
    }

    function boolean bit(int x, int j) {
        if ((x & twoToThe[j]) = 0) {
            return false;
        } else {
            return true;
        }
    }

    function int divide(int x, int y) {
        var int absX, absY;
        var boolean isXPlus, isYPlus;

        let absX = Math.abs(x);
        let absY = Math.abs(y);

        let isXPlus = (x = absX);
        let isYPlus = (y = absY);
        if ((isXPlus & isYPlus) | ~(isXPlus | isYPlus)) {
            // same sign
            return Math.naturalDivide(absX, absY);
        } else {
            // different sign
            return -Math.naturalDivide(absX, absY);
        }
    }

    function int naturalDivide(int x, int y) {
        var int q;

        if (x < y) {
            let divisionTemp = 0;
            return 0;
        }

        let q = Math.naturalDivide(x, y + y);
        let q = q + q;
        if (x - divisionTemp < y) {
            return q;
        } else {
            let divisionTemp = divisionTemp + y;
            return q + 1;
        }
    }

    function int min(int x, int y) {
        if (x > y) {
            return y;
        } else {
            return x;
        }
    }

    function int max(int x, int y) {
        if (x > y) {
            return x;
        } else {
            return y;
        }
    }

    function int sqrt(int x) {
        var int y, j, tmp, tmpSquared;

        let j = 7;
        while (j > -1) {
            let tmp = y + twoToThe[j];
            let tmpSquared = tmp * tmp;
            if ((tmpSquared - 1 < x) & (tmpSquared > 0)) {
                let y = tmp;
            } 

            let j = j - 1;
        }

        return y;
    }
}