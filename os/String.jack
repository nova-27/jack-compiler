class String {
    field Array chars;
    field int len;

    constructor String new(int maxLength) {
        let chars = Array.new(maxLength);
        let len = 0;
        return this;
    }

    method void dispose() {
        do chars.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method int length() {
        return len;
    }

    method char charAt(int j) {
        return chars[j];
    }

    method void setCharAt(int j, char c) {
        let chars[j] = c;
        return;
    }

    method String appendChar(char c) {
        let chars[len] = c;
        let len = len + 1;
        return this;
    }

    method void eraseLastChar() {
        let len = len - 1;
        return;
    }

    method int intValue() {
        var int val, i;
        var boolean isMinus;

        if (chars[0] = 45) {
            let isMinus = true;
            let i = 1;
        }

        while (i < len) {
            let val = val * 10 + chars[i] - 48;
            let i = i + 1;
        }

        if (isMinus) {
            let val = -val;
        }

        return val;
    }

    method void setInt(int val) {
        if (Math.bit(val, 15)) {
            // minus
            do appendChar(45);
            let val = -val;
        }

        do setNaturalInt(val);

        return;
    }

    method void setNaturalInt(int val) {
        var int lastDigit;

        let lastDigit = Math.naturalRemainder(val, 10) + 48;

        if (val < 10) {
            do appendChar(lastDigit);
        } else {
            do setInt(val / 10);
            do appendChar(lastDigit);
        }

        return;
    }

    function char newLine() {
        return 10;
    }

    function char backSpace() {
        return 8;
    }

    function char doubleQuote() {
        return 34;
    }
}
